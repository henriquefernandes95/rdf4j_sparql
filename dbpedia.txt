#QUERY
#Obtém as classes utilizadas por sujeitos e objetos anotadas com AGROVOC.
PREFIX lodbr: <http://lodbr.ufrj.br/agrotoxicos/propriedade/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
select distinct ?ClasseConceito1 ?ClasseConceito2 where {
    {
    	service <http://localhost:7200/repositories/D1> {
            ?s ?p ?o .
            #Extrai sujeitos
            FILTER (isURI(?s) && !(strstarts(str(?s), str("http://www.w3.org"))) && contains(str(?s), "agrovoc")) .
            #Esta string com endpoint pode ser parametrizada
            BIND (uri("http://agrovoc.uniroma2.it/sparql") AS ?vEndpoint ) .
    	}

        service ?vEndpoint {
            ?s skos:broader ?Classe .
            ?Classe skos:prefLabel ?ClasseConceito1 .
            ?s skos:closeMatch ?ClasseDBpedia .
            FILTER langMatches( lang(?ClasseConceito1), "PT" ) .
        }
        service <http://dbpedia.org/sparql> {
            ?ClasseDBpedia rdf:type ?ClasseConceito2 .
        }
}
union {
        service <http://localhost:7200/repositories/D1> {
            ?s ?p ?o .
            #Extrai objetos
            FILTER (isURI(?o) && !(strstarts(str(?o), str("http://www.w3.org"))) && contains(str(?o), "agrovoc")) .
            #Esta string com endpoint pode ser parametrizada
            BIND (uri("http://agrovoc.uniroma2.it/sparql") AS ?vEndpoint ) .
        }

        service ?vEndpoint {
            ?o skos:broader ?Classe .
            ?Classe skos:prefLabel ?ClasseConceito1 .
            ?o skos:closeMatch ?ClasseDBpedia .
            FILTER langMatches( lang(?ClasseConceito1), "PT" ) .
        }
        service <http://dbpedia.org/sparql> {
            ?ClasseDBpedia rdf:type ?ClasseConceito2 .
        }
}
}
#QUERY
#Obtém as classes utilizadas por sujeitos e objetos anotadas com CHEBI.
PREFIX lodbr: <http://lodbr.ufrj.br/agrotoxicos/propriedade/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
select distinct ?ClasseConceito1 where {
    {
    	service <http://192.168.1.102:7200/repositories/D3> {
            ?s ?p ?o .
            #Extrai sujeitos
            FILTER (isURI(?s) && !(strstarts(str(?s), str("http://www.w3.org"))) && contains(lcase(str(?s)), "chebi")) .
            BIND (uri("http://sparql.hegroup.org/sparql/") AS ?vEndpoint ) .
    	}

        service ?vEndpoint {
            ?s rdfs:subClassOf ?Classe .
            ?Classe rdfs:label ?ClasseConceito1 .
        }
}
union {
        service <http://192.168.1.102:7200/repositories/D3> {
            ?s ?p ?o .
            #Extrai objetos
            FILTER (isURI(?o) && !(strstarts(str(?o), str("http://www.w3.org"))) && contains(lcase(str(?o)), "chebi")) .
            BIND (uri("http://sparql.hegroup.org/sparql/") AS ?vEndpoint ) .
        }

        service ?vEndpoint {
            ?o rdfs:subClassOf ?Classe .
            ?Classe rdfs:label ?ClasseConceito1 .
        }

}
}

